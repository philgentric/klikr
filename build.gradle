/*
 JVM "classic" build for klik

0. cp build.gradle.jvm build.gradle

1. make sure you have a JDK with javafx bundled:

sdk install java 25.fx-zulu

2. Enjoy!

start klik 'standalone' with:

gradle klik

start audio_player 'standalone' with:

gradle audio_player

start the launcher with:

gradle run

COSMETIC: to get 'live' transmission
of UI properties (style, font, etc) to work,
you need to start the launcher first as it acts
as a dispatcher for these properties.


*/


plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.0.1'
    //id 'org.beryx.jlink' version '2.24.0'       // Beryx jlink plugin
}

java {
    sourceCompatibility = '25'
    targetCompatibility = '25'
}



version = '1.0' // application_version
group = 'klikr'


javafx {
    version = "25"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml','javafx.media','javafx.web' ]
}

repositories {
    mavenCentral()
}

dependencies {
    // EXIF reader
    implementation 'com.drewnoakes:metadata-extractor:2.19.0'
    // Cache
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.2'
    // moving folders across different drives
    implementation group: 'commons-io', name: 'commons-io', version: '2.19.0'
    // for writing png files without AWT
    implementation 'ar.com.hjg:pngj:2.1.0'
    // for VIPS image rescaling
    implementation("app.photofox.vips-ffm:vips-ffm-core:1.9.1")
}

tasks.register('klikr', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.Klikr_application'
    classpath(sourceSets.main.runtimeClasspath)
    group = 'applications'
    //jvmArgs = ['--enable-native-access=ALL-UNNAMED', '-Djava.library.path=/usr/local/lib:/opt/homebrew/Cellar/vips/8.17.2/lib']

    doFirst {
        println 'doFirst start'

        def props   = new Properties()
        def dir     = new File(System.getProperty('user.home'), '.klikr')
        def file    = new File(dir, 'ram.properties')

        // 1️⃣ Make sure the file exists
        if (!file.exists())
        {
            println "${file} not found – using default JVM arguments."
            return;
        }

        // Load it safely
        try {
            file.withInputStream { stream -> props.load(stream) }
            println "doFirst props loaded: ${props}"
        } catch (Exception e) {
            println "Failed to load ${file}: ${e.message}"
            // Optional: decide whether to abort or continue
        }
        // Pull the property, validate it, and set the flag
        final int DEFAULT_RAM_GB = 2   // whatever feels sane for your project

        def raw = props.getProperty('max_RAM_in_GBytes')
        if (!raw) {
            println "Property 'max_RAM_in_GBytes' missing – using default (${DEFAULT_RAM_GB}G)."
            return
        }

        // try to parse the value
        Integer ramInGb
        try {
            ramInGb = raw.toInteger()
        } catch (NumberFormatException nfe) {
            println "'max_RAM_in_GBytes' is not an integer: '${raw}'. Ignoring."
            return
        }

        // validate the range
        if (ramInGb <= 0) {
            println "'max_RAM_in_GBytes' must be > 0 – got ${ramInGb}. Ignoring."
            return
        }

        // All good – set the flag
        println "doFirst found max_RAM_in_GBytes: ${ramInGb}G"
        jvmArgs "-Xmx${ramInGb}G"

        // If you need to return something from doFirst, put it here.
        return
    }
}


tasks.register('audio_player', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.audio.Audio_player_application'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}

tasks.register('song_playlist_app', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.audio.Song_playlist_app'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}

tasks.register('simple_audio', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.audio.new_player.Simple_audio_player_application'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}

tasks.register('image_playlist_app', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.experimental.image_playlist.Image_playlist_app'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}

tasks.register('in3D', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.in3D.The_main_circular_3'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}


tasks.register('mmap', JavaExec) {
    dependsOn 'classes'
    mainClass = 'klikr.util.mmap.Mmap_test'
    classpath = sourceSets.main.runtimeClasspath
    group = 'applications'
    return
}


application {
    mainClass = "klikr.Launcher"
}

run{
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
    //systemProperty 'java.library.path' , '/usr/local/lib:/opt/homebrew/Cellar/vips/8.17.2/lib'
    //jvmArgs = ['--enable-native-access=ALL-UNNAMED','-Djava.library.path=/usr/local/lib:/opt/homebrew/Cellar/vips/8.17.2/lib']
}


shadowJar {
    archiveFileName.set("klikr.jar")
    manifest {
        attributes(
                'Main-Class': 'klikr.Klikr_application',
                'Implementation-Version' : project.version,
                'JavaFX-Modules': ['javafx.base','javafx.graphics','javafx.controls','javafx.fxml', 'javafx.media', 'javafx.web']
        )
    }
    mergeServiceFiles()
    //dependencies {
    //    include(dependency('ar.com.hjg:pngj:2.1.0'))
    //}
    //relocate 'ar.com.hjg.pngj', 'klik.ar.com.hjg.pngj'
}

